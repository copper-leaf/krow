buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.vladsch.flexmark:flexmark:0.34.18'
    }
}

import com.vladsch.flexmark.html.HtmlRenderer
import com.vladsch.flexmark.parser.Parser
import com.vladsch.flexmark.util.options.MutableDataSet

// Get release info
//----------------------------------------------------------------------------------------------------------------------

tag {
    message { "Bump version to ${version}" }
}

task getReleaseName {
    doLast {
        println project.version.toString()
    }
}

task getReleaseNotes {
    doLast {
        def changelogProject = project(rootProject.ext.changelogProject)
        def versionFilename = changelogProject.version.toString().replaceAll("\\.", "_") + ".md"
        def fullVersionFilename = "${changelogProject.projectDir}/src/orchid/resources/changelog/$versionFilename"
        def versionFile = file(fullVersionFilename)

        if (versionFile.exists()) {
            def changelogMarkdown = versionFile.text.split("---").last().trim()

            def options = new MutableDataSet()
            def parser = Parser.builder(options).build()
            def renderer = HtmlRenderer.builder(options).build()

            def changelogHtml = renderer.render(parser.parse(changelogMarkdown))
            changelogHtml = changelogHtml.replaceAll("\n", " ")
            println changelogHtml
        } else {
            println "No release notes for $versionFilename"
        }
    }
}

// Deploy
//----------------------------------------------------------------------------------------------------------------------

task deploy {
    doLast {}
}
rootProject.tasks.deploy.dependsOn rootProject.ext.mainProjects*.publish

// Code Coverage Reports
//----------------------------------------------------------------------------------------------------------------------
task codeCoverageReport(type: JacocoReport) {
    dependsOn rootProject.ext.mainProjects*.test

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    rootProject.ext.mainProjects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

configurations { codacy }
dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:4.0.2'
}
task sendCoverageToCodacy(type: JavaExec) {
    dependsOn codeCoverageReport

    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l", "Java",
            "-r", "${buildDir}/reports/jacoco/report.xml",
            "-t", "${System.getenv("CODACY_PROJECT_TOKEN")}",
            "--language", "Kotlin"
    ]
}
