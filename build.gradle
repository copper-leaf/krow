// Configure build
//----------------------------------------------------------------------------------------------------------------------

buildscript {
    ext.kotlin_version = '1.2.60'
    ext.dokka_version = '0.9.17'
    ext.orchid_version = '0.9.3'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "gradle.plugin.com.eden:orchidPlugin:$orchid_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.eden.orchidPlugin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// Configure Project
//----------------------------------------------------------------------------------------------------------------------

group 'com.eden'

sourceCompatibility = JavaVersion.VERSION_1_6
compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/javaeden/Orchid/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"

    // testing
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.1.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.1.0"
    testCompile "io.strikt:strikt-core:0.11.0"

    // Orchid
    def orchid_version = '0.9.3'
    orchidCompile "io.github.javaeden.orchid:OrchidCore:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidChangelog:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidBsDoc:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidPages:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidPluginDocs:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidSearch:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidSyntaxHighlighter:$orchid_version"
    orchidRuntime "io.github.javaeden.orchid:OrchidWiki:$orchid_version"
}

// Javadoc and Orchid
//----------------------------------------------------------------------------------------------------------------------

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from project.sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: orchidBuild) {
    classifier = 'javadoc'
    from "${project.buildDir}/docs/orchid"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

if(project.sourceSets.main.allJava.files?.empty) {
    project.tasks.assemble.dependsOn orchidBuild
    project.tasks.orchidBuild.mustRunAfter javadoc
}

// Testing and Code Coverage
//----------------------------------------------------------------------------------------------------------------------

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

// Get release info
//----------------------------------------------------------------------------------------------------------------------

tag {
    message { "Bump version to ${version}" }
}

task getReleaseName {
    doLast {
        println project.version.toString()
    }
}

task getReleaseNotes {
    doLast {
        def versionFilename = project.version.toString().replaceAll("\\.", "_") + ".md"
        def fullVersionFilename = "$projectDir/src/orchid/resources/changelog/$versionFilename"
        def versionFile = file(fullVersionFilename)

        if(versionFile.exists()) {
            println versionFile.text.split("---").last().trim()
        }
        else {
            println "No release notes"
        }
    }
}

// Publish to Bintray
//----------------------------------------------------------------------------------------------------------------------

afterEvaluate { project ->
    def pomConfig = {
        scm {
            url 'https://github.com/JavaEden/Krow.git'
            connection 'https://github.com/JavaEden/Krow.git'
            developerConnection 'https://github.com/JavaEden/Krow.git'
        }
        licenses {
            license {
                name 'MIT'
                url 'https://opensource.org/licenses/mit'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'JavaEden'
                name 'Casey Brooks'
                email 'cjbrooks12@gmail.com'
            }
        }
    }

    project.publishing {
        publications {
            KrowPublication(MavenPublication) {
                from project.components.java
                artifact project.sourcesJar
                artifact project.javadocJar
                groupId "${project.group}"
                artifactId "${project.name}"
                version "${project.version}"
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'Simple yet powerful logging and string formatting for Java')
                    root.appendNode('name', 'Krow')
                    root.appendNode('url', 'https://javaeden.github.io/Krow')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = "${project.properties['bintray_username']}"
        key = "${project.properties['bintray_apiKey']}"
        publications = ['KrowPublication']

        dryRun = project.hasProperty('dryDeploy')
        publish = !project.hasProperty('dryDeploy')
        override = true

        pkg {
            repo = "Eden"
            name = "${project.name}"
            userOrg = 'javaeden'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/JavaEden/Krow.git'

            version {
                name = "${project.version}"
                desc = "Krow ${project.name} ${project.version}"
                released = new Date()

                gpg {
                    sign = true
                }
                mavenCentralSync {
                    sync = (project.hasProperty('includeMavenCentralSync'))
                    user = "${project.properties['mavenCentral_username']}"
                    password = "${project.properties['mavenCentral_password']}"
                }
            }
        }
    }
}

project.tasks.publish.dependsOn bintrayUpload

task deploy {
    dependsOn project.tasks.publish
    doLast { }
}

// dokka config
//----------------------------------------------------------------------------------------------------------------------

def _dokkaRoot = "$buildDir/docs/dokka"
def _dokkaOrchidRoot = "${project.rootDir}/src/orchid/resources/pages/api"

dokka {
    outputFormat = 'html'
    outputDirectory = _dokkaRoot
    reportUndocumented = false

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/copper-leaf/krow/blob/master/src/main/kotlin"
        suffix = "#L"
    }
}
task dokkaPreOrchid {
    dependsOn dokka
    doLast {
        def outputExtension = 'html'

        fileTree(_dokkaRoot).visit { FileVisitDetails details ->
            if (details.isDirectory()) return
            if (!details.path.endsWith(outputExtension)) return

            def originalFile = details.file
            def originalPath = originalFile.path
            def newPath = originalPath
            newPath = newPath.replaceAll("${_dokkaRoot}/${project.name}", '')
            newPath = newPath.replaceAll('\\.' + outputExtension, '')
            newPath = newPath.replaceAll('\\.', '/')
            newPath = newPath + "." + outputExtension
            newPath = _dokkaOrchidRoot + newPath

            File newFile = new File(newPath)
            if (!newFile.parentFile.exists()) {
                newFile.parentFile.mkdirs()
            }
            newFile.createNewFile()

            def originalText = originalFile.text
            String newText = originalText
            newText = newText.replaceAll("\\.md", ".html")
            newText = newText.replaceAll("<HTML>", "")
            newText = newText.replaceAll("</HTML>", "")
            newText = newText.replaceAll("(?s)<HEAD>.*</HEAD>", "")
            newText = newText.replaceAll("<BODY>", "")
            newText = newText.replaceAll("</BODY>", "")
            newText = '<div class="dokka-docs">' + newText + '</div>'

            newFile << newText
        }

        def packageListFile = file("${_dokkaRoot}/${project.name}/package-list")
        packageListFile.write '---\nrenderMode: raw\nusePrettyUrl: false\n---\n' + packageListFile.text
        packageListFile.renameTo("${_dokkaOrchidRoot}/package-list")

        copy {
            from file("${_dokkaRoot}/style.css")
            into file("${project.rootDir}/src/orchid/resources/assets/dokka/")
            rename '(.+)\\.css', '$1.scss'

            eachFile { fileCopyDetails ->
                fileCopyDetails.file.write ".dokka-docs {\n" + fileCopyDetails.file.text + "\n}"
            }
        }
    }
}

task dokkaPostOrchid {
    doLast {
        file(_dokkaOrchidRoot).deleteDir()
        file("${project.rootDir}/src/orchid/resources/assets/dokka/style.scss").delete()
    }
}

// Orchid config
//----------------------------------------------------------------------------------------------------------------------

orchid {
    version = "${project.version}"
    theme = "BsDoc"

    if (project.hasProperty('env') && project.property('env') == 'prod') {
        baseUrl = "https://copper-leaf.github.io/krow"
        environment = 'prod'
    } else {
        baseUrl = "http://localhost:8080"
        environment = 'debug'
    }

    args = ["githubToken ${project.hasProperty("github_token") ? project.property("github_token") : System.getenv('GITHUB_TOKEN')}"]
}
tasks.orchidBuild.dependsOn dokkaPreOrchid
tasks.orchidServe.dependsOn dokkaPreOrchid
tasks.orchidDeploy.dependsOn dokkaPreOrchid

tasks.build.dependsOn orchidBuild
tasks.clean.dependsOn dokkaPostOrchid
tasks.check.dependsOn jacocoTestReport

tasks.deploy.dependsOn orchidDeploy
tasks.orchidDeploy.dependsOn publish
tasks.publish.dependsOn bintrayUpload

// Code Coverage Reports
//----------------------------------------------------------------------------------------------------------------------
task codeCoverageReport(type: JacocoReport) {
    dependsOn test

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
    sourceSets sourceSets.main

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

configurations { codacy }
dependencies {
    codacy 'com.github.codacy:codacy-coverage-reporter:4.0.2'
}
task sendCoverageToCodacy(type: JavaExec) {
    dependsOn codeCoverageReport

    main = "com.codacy.CodacyCoverageReporter"
    classpath = configurations.codacy
    args = [
            "report",
            "-l", "Java",
            "-r", "${buildDir}/reports/jacoco/report.xml",
            "-t", "${System.getenv("CODACY_PROJECT_TOKEN")}",
            "--language", "Kotlin"
    ]
}
